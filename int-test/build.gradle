plugins {
    id 'groovy'
}

repositories {
    mavenCentral()
}

def intTest = sourceSets.create('intTest')

configurations[intTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)
configurations[intTest.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)

tasks.named('test') {
    useJUnitPlatform()
}

def intTestTask = tasks.register('intTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform()

    testClassesDirs = intTest.output.classesDirs
    classpath = configurations[intTest.runtimeClasspathConfigurationName] + intTest.output

    shouldRunAfter(tasks.named('test'))
}

dependencies {
    intTestImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
    intTestImplementation "org.spockframework:spock-core"
    intTestImplementation "com.google.code.gson:gson:2.10.1"
    intTestImplementation "org.apache.kafka:kafka-clients:3.5.0"

}
